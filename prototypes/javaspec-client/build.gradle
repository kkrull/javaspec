//Code that uses JavaSpec's API to write specs
plugins {
	id 'java'
	id 'com.adarshr.test-logger'
	id 'com.diffplug.spotless'
}

dependencies {
	testImplementation project(':javaspec-api')
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

	testRuntimeOnly project(':javaspec-engine')
//  testRuntimeOnly project(':javaspec-engine-discovery-request-listener')
//  testRuntimeOnly project(':jupiter-test-execution-listener')
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

//Suspected Gradle issue Gradle groups tests by 'default-package' and 'UnknownClass'.
//Related Github issue: https://github.com/cucumber/cucumber-jvm/issues/2176
test {
	useJUnitPlatform {
		//Jupiter will find this engine even if it's omitted, as long as it's on the classpath.
		includeEngines 'javaspec-engine'
	}

	//Show all output: the specs themselves *and* the TestEngine
	onOutput { _descriptor, TestOutputEvent engineEvent ->
		logger.lifecycle(engineEvent.message.trim())
	}
}

//https://github.com/radarsh/gradle-test-logger-plugin
testlogger {
	theme 'mocha-parallel' //Override at runtime with: -Dtestlogger.theme=plain-parallel
	showSimpleNames true
}

version '0.0.1'
