//The version of JavaSpec that uses nested lambdas to organize specs
plugins {
	id 'com.adarshr.test-logger' version '3.1.0' apply false

	//https://github.com/diffplug/spotless/tree/main/plugin-gradle#java
	id 'com.diffplug.spotless' version '6.3.0' apply false
}

description 'JavaSpec 2.x'

subprojects {
	group 'info.javaspec'

	repositories {
		mavenCentral()
	}

	//Customize at runtime ./gradlew -Dtestlogger.theme=plain ...
	apply plugin: 'com.adarshr.test-logger'
	testlogger {
		theme 'mocha'
	}

	apply plugin: 'com.diffplug.spotless'
	spotless {
		java {
			removeUnusedImports()
			importOrder()
			eclipse().configFile('../etc/eclipse-format.xml')
		}
	}
}

task installGitHook(type: Copy) {
	description 'Installs pre-commit hook in .git/hooks'
	from 'etc/git-hooks'
	into '../.git/hooks'

	doLast {
		println "Installed Git hooks in ${destinationDir}"
	}
}
