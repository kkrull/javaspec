//Conventions for Java projects that publish Maven artifacts
plugins {
	id 'maven-publish'
	id 'signing'
}

import info.javaspec.MavenPublishConventionExtension
def extension = project.extensions.create('mavenPublishConvention', MavenPublishConventionExtension)

//https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
	publications {
		maven(MavenPublication) {
			afterEvaluate {
				from extension.publicationFrom.get()
			}

			pom {
				afterEvaluate {
					description = extension.publicationDescription.get()
					name = extension.publicationName.get()
				}

				developers {
					developer {
						id = 'kkrull'
						email = 'kdkrull@gmail.com'
						name = 'Kyle Krull'
						organizationUrl = 'https://github.com/kkrull'
					}
				}

				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/kkrull/javaspec/issues'
				}

				licenses {
					license {
						name = 'MIT license'
						url = 'https://spdx.org/licenses/MIT'
					}
				}

				scm {
					connection = 'scm:git:git://github.com/kkrull/javaspec.git'
					developerConnection = 'scm:git:ssh://github.com:kkrull/javaspec.git'
					url = 'https://github.com/kkrull/javaspec'
				}

				url = 'https://javaspec.info'
			}
		}
	}

	repositories {
		maven {
			name 'sonatype'
			url = 'https://oss.sonatype.org/content/repositories/snapshots'
			credentials {
				afterEvaluate {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
}

//https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
	sign publishing.publications['maven']
}

task signingCheckConfig() {
	doLast {
		println 'Checking GPG configuration...'
		println '- signing.bogus: ' + project.hasProperty('signing.bogus')
		println '- signing.gradleProperty: ' + project.hasProperty('signing.gradleProperty')
		println '- signing.runtimeProperty: ' + project.hasProperty('signing.runtimeProperty')
	}
}

signMavenPublication.dependsOn signingCheckConfig
