//Conventions for Java projects that publish Maven artifacts
plugins {
	id 'maven-publish'
}

import info.javaspec.MavenPublishConventionExtension
def extension = project.extensions.create('mavenPublishConvention', MavenPublishConventionExtension)

//https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
	publications {
		maven(MavenPublication) {
			afterEvaluate {
				from extension.publicationFrom.get()
			}

			pom {
				afterEvaluate {
					description = extension.publicationDescription.get()
					name = extension.publicationName.get()
				}

				developers {
					developer {
						id = 'kkrull'
						email = 'kdkrull@gmail.com'
						name = 'Kyle Krull'
						organizationUrl = 'https://github.com/kkrull'
					}
				}

				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/kkrull/javaspec/issues'
				}

				licenses {
					license {
						name = 'MIT license'
						url = 'https://spdx.org/licenses/MIT'
					}
				}

				scm {
					connection = 'scm:git:git://github.com/kkrull/javaspec.git'
					developerConnection = 'scm:git:ssh://github.com:kkrull/javaspec.git'
					url = 'https://github.com/kkrull/javaspec'
				}

				url = 'https://javaspec.info'
			}
		}
	}

	repositories {
		maven {
			name 'sonatype-oss'
			url = 'https://oss.sonatype.org/content/repositories/snapshots'
		}
	}
}
